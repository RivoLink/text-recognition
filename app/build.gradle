/*
 * Text Recognition
 * Uses neural-network library for MNIST/EMNIST character recognition
 */

plugins {
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
    // Neural Network library
    implementation files('../libs/neural-network/dist/neural-network.jar')
    
    // Testing
    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'mg.rivolink.Main'
}

// Verify neural-network JAR exists before compilation
tasks.compileJava {
    doFirst {
        def jarFile = file('../libs/neural-network/dist/neural-network.jar')
        if (!jarFile.exists()) {
            throw new GradleException("neural-network.jar not found")
        }
        println "Using neural-network.jar: ${jarFile.name}"
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = "full"
        showStandardStreams = false
    }
}

// Neural Network JAR rebuild
task rebuildNeuralNetwork(type: Exec) {
    group = 'build'
    description = 'Rebuild neural-network.jar from source'
    workingDir '../libs/neural-network'
    
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        commandLine 'bash', 'scripts/build.sh', '--clean', '--target=21'
    } else {
        commandLine './scripts/build.sh', '--clean', '--target=21'
    }
}

// gradle :app:runDatasetDownloader -Pdataset=mnist
task runDatasetDownloader(type: JavaExec) {
    group = 'application'
    description = 'Download MNIST/EMNIST dataset (override with -Pdataset=mnist|emnist)'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'mg.rivolink.mnist.task.MNISTDownloaderTask'
    dependsOn 'classes'

    if (project.hasProperty('dataset')) {
        args project.property('dataset')
    }
}

task runModelTrainer(type: JavaExec) {
    group = 'application'
    description = 'Train MNIST/EMNIST model (override with -Pdataset=mnist|emnist)'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'mg.rivolink.mnist.task.MNISTTrainerTask'
    dependsOn 'classes'

    if (project.hasProperty('dataset')) {
        args project.property('dataset')
    }
}

task runModelEvaluator(type: JavaExec) {
    group = 'application'
    description = 'Evaluate MNIST/EMNIST model (override with -Pdataset=mnist|emnist)'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'mg.rivolink.mnist.task.MNISTEvaluatorTask'
    dependsOn 'classes'

    if (project.hasProperty('dataset')) {
        args project.property('dataset')
    }
}

task runImageSegmenter(type: JavaExec) {
    group = 'application'
    description = 'Segments image and shows detected characters (override with -Pimage=<path>)'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'mg.rivolink.image.demo.ImageSegmenterDemo'
    dependsOn 'classes'

    if ((args == null || args.isEmpty()) && project.hasProperty('image')) {
        args project.property('image')
    }
}

task runImagePreprocessor(type: JavaExec) {
    group = 'application'
    description = 'Pre-process image by applying morphological operations (override with -Pimage=<path>)'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'mg.rivolink.image.demo.ImagePreprocessorDemo'
    dependsOn 'classes'

    if ((args == null || args.isEmpty()) && project.hasProperty('image')) {
        args project.property('image')
    }
}

task runLanguageModelDemo(type: JavaExec) {
    group = 'application'
    description = 'Demo of a language model for word validation and spelling correction'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'mg.rivolink.lang.demo.LanguageModelDemo'
    dependsOn 'classes'
}

tasks.register("runTextRecognition", JavaExec) {
    group = 'application'
    description = 'Recognize text from image (use -Pdataset=mnist|emnist, default emnist; -Pimage=<path> required)'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'mg.rivolink.Main'
    dependsOn 'classes'

    if ((args == null || args.isEmpty()) && project.hasProperty('image')) {
        def dataset = project.hasProperty('dataset') ? project.property('dataset') : 'emnist'
        def image = project.property('image') as String

        args = [dataset, image]
    }
}
