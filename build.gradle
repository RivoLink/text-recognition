plugins {
    id 'java'
}

allprojects {
    group = 'mg.rivolink'
    version = '1.0.0'
    
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }
    
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

// Delegate tasks to app
task run {
    group = 'application'
    description = 'Run the application'
    dependsOn ':app:run'
}

task downloadDataset(type: DefaultTask) {
    group = 'application'
    description = 'Run the dataset downloader (override dataset with -Pdataset=mnist|emnist)'
    dependsOn ':app:runDatasetDownloader'
}

task trainModel(type: DefaultTask) {
    group = 'application'
    description = 'Run the model trainer (override dataset with -Pdataset=mnist|emnist)'
    dependsOn ':app:runModelTrainer'
}

task evaluateModel(type: DefaultTask) {
    group = 'application'
    description = 'Run the model evaluator (override dataset with -Pdataset=mnist|emnist)'
    dependsOn ':app:runModelEvaluator'
}

task retrainModel(type: DefaultTask) {
    group = 'application'
    description = 'Resume training an existing model (override dataset with -Pdataset=mnist|emnist, -Pepochs=<n>)'
    dependsOn ':app:runModelRetrainer'
}

tasks.register("segmentImage") {
    group = 'application'
    description = 'Run the image segmenter (override image with -Pimage=<path>)'
    dependsOn ':app:runImageSegmenter'

    if (project.hasProperty('image')) {
        def path = project.property('image') as String
        def file = new File(path)

        file = file.isAbsolute() ? file : new File(project.projectDir, path);

        project(":app").tasks.named("runImageSegmenter").configure {
            args = [file.absolutePath]
        }
    }
}

tasks.register("preprocessImage") {
    group = 'application'
    description = 'Run the image preprocessor (override image with -Pimage=<path>)'
    dependsOn ':app:runImagePreprocessor'

    if (project.hasProperty('image')) {
        def path = project.property('image') as String
        def file = new File(path)

        file = file.isAbsolute() ? file : new File(project.projectDir, path);

        project(":app").tasks.named("runImagePreprocessor").configure {
            args = [file.absolutePath]
        }
    }
}

task demoLanguageModel(type: DefaultTask) {
    group = 'application'
    description = 'Run the language model demo'
    dependsOn ':app:runLanguageModelDemo'
}

tasks.register("recognizeText") {
    group = 'application'
    description = 'Run text recognition (use -Pdataset=mnist|emnist, default emnist; -Pimage=<path> required)'
    dependsOn ':app:runTextRecognition'

    if (project.hasProperty('image')) {
        def dataset = project.hasProperty('dataset') ? project.property('dataset') : 'emnist'
        def path = project.property('image') as String

        def file = new File(path)
        file = file.isAbsolute() ? file : new File(project.projectDir, path)

        project(":app").tasks.named("runTextRecognition").configure {
            args = [dataset, file.absolutePath]
        }
    }
}
